package xml

import concretesyntax.Scala
import xml.XmlSyntax
import xml.XmlTextTools

sugar Sugar {
	context-free syntax
		Element -> ScalaExpr {prefer}
		Element -> ScalaNoLExpr {prefer}

	desugarings
		desugar-element
		desugar-empty-element
		desugar-attributes

	rules
		desugar-element:
			Element(ElemName(QName(None(), name)), [], [], _) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					scala.xml.Null,
					scala.xml.TopScope,
					false) ]|
				where quotedName := <quote(!'"')> name

		desugar-element:
			Element(ElemName(QName(None(), name)), attrs, [], _) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					:${attrs},
					scala.xml.TopScope,
					false) ]|
				where quotedName := <quote(!'"')> name

		desugar-element:
			Element(ElemName(QName(None(), name)), [], children, _) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					scala.xml.Null,
					scala.xml.TopScope,
					false,
					:${children}) ]|
				where quotedName := <quote(!'"')> name

		desugar-empty-element:
			EmptyElement(ElemName(QName(None(), name)), []) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					scala.xml.Null,
					scala.xml.TopScope,
					true) ]|
				where quotedName := <quote(!'"')> name

		desugar-empty-element:
			EmptyElement(ElemName(QName(None(), name)), attrs) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					:${attrs},
					scala.xml.TopScope,
					true) ]|
				where quotedName := <quote(!'"')> name

		desugar-attributes:
			[Attribute(AttrName(QName(None(), name)), cdata) | []] ->
				|[ new scala.xml.UnprefixedAttribute(
					:${<quote(!'"')> name},
					:${<quote(!'"')> <xml-attr-value2string> cdata},
					scala.xml.Null) ]|

		desugar-attributes:
			[Attribute(AttrName(QName(None(), name)), cdata) | rst] ->
				|[ new scala.xml.UnprefixedAttribute(
					:${<quote(!'"')> name},
					:${<quote(!'"')> <xml-attr-value2string> cdata},
					:${rst}) ]|
}
