package xml

import concretesyntax.Scala
import xml.XmlSyntax
import xml.XmlTextTools

sugar Sugar {
	lexical syntax
		"<!--"  -> ScalaPlainId {reject}
		"-->"   -> ScalaPlainId {reject}

	context-free syntax
		Element -> ScalaExpr {prefer}
		Element -> ScalaNoLExpr {prefer}

	desugarings
		desugar-element
		desugar-empty-element
		desugar-attributes
		desugar-text

	strategies
		scala-multiline-quote = double-quote; double-quote; double-quote

	rules
		desugar-element:
			Element(ElemName(QName(None(), name)), [], [], _) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					scala.xml.Null,
					scala.xml.TopScope,
					false) ]|
				where quotedName := <quote(!'"')> name

		desugar-element:
			Element(ElemName(QName(None(), name)), attrs, [], _) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					:${attrs},
					scala.xml.TopScope,
					false) ]|
				where quotedName := <quote(!'"')> name

		desugar-element:
			Element(ElemName(QName(None(), name)), [], children, _) ->
				AppExpr(
				  Path(["scala", "xml", "Elem"])
				, ArgumentExprs(
				    Some(
				      Exprs(
				        <flatten-list>
				        [ Null()
				        , <double-quote> name
				        , Path(["scala", "xml", "Null"])
				        , Path(["scala", "xml", "TopScope"])
				        , False()
				        , children ]))))

		desugar-element:
			Element(ElemName(QName(None(), name)), attrs, children, _) ->
				AppExpr(
				  Path(["scala", "xml", "Elem"])
				, ArgumentExprs(
				    Some(
				      Exprs(
				        <flatten-list>
				        [ Null()
				        , <double-quote> name
				        , attrs
				        , Path(["scala", "xml", "TopScope"])
				        , False()
				        , children ]))))

		desugar-empty-element:
			EmptyElement(ElemName(QName(None(), name)), []) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					scala.xml.Null,
					scala.xml.TopScope,
					true) ]|
				where quotedName := <quote(!'"')> name

		desugar-empty-element:
			EmptyElement(ElemName(QName(None(), name)), attrs) ->
				|[ scala.xml.Elem(
					null,
					:${quotedName},
					:${attrs},
					scala.xml.TopScope,
					true) ]|
				where quotedName := <quote(!'"')> name

		desugar-attributes:
			[Attribute(AttrName(QName(None(), name)), cdata) | []] ->
				|[ new scala.xml.UnprefixedAttribute(
					:${<quote(!'"')> name},
					:${<quote(!'"')> <xml-attr-value2string> cdata},
					scala.xml.Null) ]|

		desugar-attributes:
			[Attribute(AttrName(QName(None(), name)), cdata) | rst] ->
				|[ new scala.xml.UnprefixedAttribute(
					:${<quote(!'"')> name},
					:${<quote(!'"')> <xml-attr-value2string> cdata},
					:${rst}) ]|

		desugar-text:
			Text(chardata) ->
				|[ scala.xml.Text(:${str}) ]|
				where str := <scala-multiline-quote> <chardata2string> Text(chardata)
}
