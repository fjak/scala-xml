package xml

import concretesyntax.Scala
import xml.XmlSyntax
import xml.XmlTextTools

sugar Sugar {
	lexical syntax
		"<!--"  -> ScalaPlainId {reject}
		"-->"   -> ScalaPlainId {reject}

	context-free syntax
		Element -> ScalaExpr {prefer}
		Element -> ScalaNoLExpr {prefer}
		ScalaBlockExpr -> Element {prefer}
		ScalaBlockExpr -> AttValue

	desugarings
		desugar-element
		desugar-empty-element
		desugar-text

	strategies
		scala-multiline-quote = double-quote; double-quote; double-quote

	rules
		desugar-element:
			Element(ElemName(QName(prefix, name)), attrs, [], _) ->
				|[ scala.xml.Elem(
					:${<desugar-prefix> prefix},
					:${quotedName},
					:${<desugar-attrs> attrs},
					scala.xml.TopScope,
					false) ]|
				where quotedName := <quote(!'"')> name

		desugar-element:
			Element(ElemName(QName(prefix, name)), attrs, children, _) ->
				AppExpr(
				  Path(["scala", "xml", "Elem"])
				, ArgumentExprs(
				    Some(
				      Exprs(
				        <flatten-list>
				        [ <desugar-prefix> prefix
				        , <double-quote> name
				        , <desugar-attrs> attrs
				        , Path(["scala", "xml", "TopScope"])
				        , False()
				        , children ]))))

		desugar-empty-element:
			EmptyElement(ElemName(QName(prefix, name)), attrs) ->
				|[ scala.xml.Elem(
					:${<desugar-prefix> prefix},
					:${quotedName},
					:${<desugar-attrs> attrs},
					scala.xml.TopScope,
					true) ]|
				where quotedName := <quote(!'"')> name

		desugar-text:
			Text(chardata) ->
				|[ scala.xml.Text(:${str}) ]|
				where str := <scala-multiline-quote> <chardata2string> Text(chardata)

		constraint-error:
			Element(sname, _, _, ename) ->
			  [(sname, "Start and end tag names are different"),
			   (ename, "Start and end tag names are different")]
			where <not(structurally-equal)> (sname, ename)

		desugar-attrs:
			[] -> |[ scala.xml.Null ]|

		desugar-attrs:
			[Attribute(AttrName(QName(None(), name)), attrval) | rst] ->
				|[ new scala.xml.UnprefixedAttribute(
					:${<double-quote> name},
					:${<desugar-attrval> attrval},
					:${<desugar-attrs> rst}) ]|

		desugar-attrs:
			[Attribute(AttrName(QName(Some(Prefix(prefix)), name)), attrval) | rst] ->
				|[ new scala.xml.PrefixedAttribute(
					:${<double-quote> prefix},
					:${<double-quote> name},
					:${<desugar-attrval> attrval},
					:${<desugar-attrs> rst}) ]|

		desugar-prefix:
			None() -> Null()

		desugar-prefix:
			Some(Prefix(p)) -> <double-quote> p

		desugar-attrval:
			BlockExpr(blk) -> BlockExpr(blk)

		desugar-attrval:
			DoubleQuoted(c) -> <double-quote> <xml-attr-value2string> DoubleQuoted(c)
}
